apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 10008
        versionName "1.0.8"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'mips', 'x86'
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext.versionCodes = ['armeabi':1, 'armeabi-v7a':2, 'mips':3, 'x86':4]

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.versionCodeOverride =
                project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
    }
    variant.mergeResources.doLast {
        copy { // Hack to support Asturian, which doesn't have an ISO 639-1 code, in pre-lollipop devices
            from "$buildDir/intermediates/res/$variant.dirName/values-b+ast"
            into "$buildDir/intermediates/res/$variant.dirName/values-es-rXA"
        }
    }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.f2prateek.progressbutton:progressbutton:2.1.0@aar'
    implementation 'ch.acra:acra:4.9.2'
    implementation project(':libraries:axmlrpc')
    implementation project(':libraries:cropper')
    implementation project(':libraries:opencv')
    implementation 'com.rmtheis:tess-two:9.0.0'
//    implementation project(':libraries:tesseract-android-tools')
    implementation project(':libraries:lttoolbox-java')
}
